plugins {
    id 'application'
}

sourceSets {
    main.java.srcDirs = ["src/"]
    test.java.srcDirs = ["test/"]
    main.resources.srcDirs = ["resources/"] 
}

mainClassName = "server.ServerLauncher"

test {
    useJUnitPlatform()
    failFast = true

    testLogging {
        // set options for log level LIFECYCLE
        events "failed"
        exceptionFormat "short"

        // remove standard output/error logging from --info builds
        // by assigning only 'failed' and 'skipped' events
        info.events = ["failed", "skipped"]
    }
}

dependencies {
    implementation project(":shared")
	runtimeOnly group: "com.badlogicgames.gdx", name: "gdx-backend-lwjgl3", version: "$gdxVersion"
	implementation group: "com.badlogicgames.gdx", name: "gdx-backend-headless", version: "$gdxVersion"
	implementation group: "com.badlogicgames.gdx", name: "gdx-ai", version: "$gdxAIVersion"

	testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: "$jupiterVersion"
	testRuntimeOnly group: "org.junit.jupiter", name: "junit-jupiter-engine", version: "$jupiterVersion"
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.main.output.resourcesDir)
    from { configurations.compile.collect { zipTree(it) } }

    manifest {
        attributes 'Main-Class': mainClassName
    }
}

// The run task added by the application plugin
// is also of type JavaExec.
tasks.withType(JavaExec) {
    // Assign all Java system properties from
    // the command line to the JavaExec task.
    systemProperties System.properties
    systemProperties.remove("java.endorsed.dirs")
}

dist.dependsOn classes

tasks.dist.doLast {

    // We copy all the dependencies .JAR into the input folder for JPackage.
    copy {
        from configurations.runtime
        into "${buildDir}/libs"
    }

}

// Determine where the bundled application is going to be.
def releasePath = "${projectDir}/bundled-APP"

// creates application bundle (executable + runtime)
task appBundle(type: Exec, dependsOn: [dist, jlink]) {

    // Overwrite previous bundled application.
    if (file(releasePath).exists()) {
        delete(file(releasePath))
    } else {
        file(releasePath).mkdirs()
    }

    def commands = [
            JPackagePath.concat("/jdk-14/bin/jpackage"),
            '--package-type', 'app-image',
            '--name', project.appName,
            '--vendor', "Argentum Online Libre",
            '--app-version', "${project.version}",
            '--dest', releasePath,
            '--runtime-image', prebuiltJRE,
            '--input', "${buildDir}/libs",
            '--main-class', project.mainClassName,
            '--main-jar', "${project.name}-${project.version}.jar"
    ]

    if (osName.contains('mac')) {
        commands << "--java-options"
        commands << "-XstartOnFirstThread"
    }

    commandLine = commands

    doLast() {

        copy {

            from files(sourceSets.main.output.resourcesDir)

            if (osName.contains('mac')) {
                into("${releasePath}/${project.appName}/Contents/Resources/")
            } else if (osName.contains('windows')) {
                into("${releasePath}/${project.appName}")
            } else if (osName.contains('linux')) {
                into("${releasePath}/${project.appName}/bin")
            }

        }

        // Debloat for Windows users.
        delete fileTree("${releasePath}/${project.appName}/bin").matching { include "*.dll" exclude "applauncher.dll" }
        delete fileTree("${releasePath}/${project.appName}/app").matching { include "api*.dll" }
        delete file("${releasePath}/${project.appName}/.jpackage.xml")

        System.out.println("Application '${project.appName}' packaged.")
        System.out.println(" -> location: ${releasePath}/${project.appName}/")
    }

}