import groovy.json.JsonSlurper

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'de.undercouch.download'
apply plugin: 'com.github.breadmoirai.github-release'
apply plugin: 'org.beryx.jlink'

sourceSets {
    main.java.srcDirs = ["src/"]
    main.resources.srcDirs = ["assets/"]
}

def osName() { System.getProperty('os.name').toLowerCase(Locale.ROOT) }

if (osName().contains('mac')) {
	run {
		jvmArgs += '-XstartOnFirstThread'
	}
}

mainClassName = "launcher.DesktopLauncher"

dependencies {
    compile project(":client")
    compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDirs)
    from { configurations.compile.collect { zipTree(it) } }

    manifest {
        attributes 'Main-Class': project.mainClassName
    }
}

dist.dependsOn classes
run.dependsOn classes

def releasesDir() { "${projectDir}/build/releases/" }
static def packingDir() { "packing/" }

static def switchConfig(platform) {
    def config
    switch (platform) {
        case "windows32":
            config = packingDir().concat("build-windows-32.json")
            break
        case "windows64":
            config = packingDir().concat("build-windows-64.json")
            break
        case "mac":
            config = packingDir().concat("build-mac.json")
            break
        case "linux64":
            config = packingDir().concat("build-linux.json")
            break
    }
    config
}

def packArgs(platform) {
    [
            "${projectDir}/packing/jdks/packr.jar",
            "--platform", platform,
            switchConfig(platform)
    ]
}

task packWindows32(type: JavaExec) {
    main = "-jar"
    args = packArgs("windows32")
}

task packWindows64(type: JavaExec) {
    main = "-jar"
    args = packArgs("windows64")
}

task packMac(type: JavaExec) {
    main = "-jar"
    args = packArgs("mac")
}

task packLinux64(type: JavaExec) {
    main = "-jar"
    args = packArgs("linux64")
}

task pack {
    dependsOn 'dist'
    dependsOn 'downloadBinaries'
    dependsOn 'packWindows32'
    dependsOn 'packWindows64'
    dependsOn 'packMac'
    dependsOn 'packLinux64'
    tasks.findByName('dist').mustRunAfter('downloadBinaries')
    tasks.findByName('packWindows32').mustRunAfter('dist')
    tasks.findByName('packWindows64').mustRunAfter 'packWindows32'
    tasks.findByName('packMac').mustRunAfter 'packWindows64'
    tasks.findByName('packLinux64').mustRunAfter 'packMac'
}

task zipAll() {
    file(releasesDir()).eachDir() { folder ->
        def taskName = "zip${folder.name.capitalize()}"
        task "$taskName"(type: Zip) {
            from folder
            archiveFile.set(project.file(zipDir() + folder.name + ".zip"))
        }
        zipAll.dependsOn taskName
    }
}

task downloadBinaries() {
    def binJson = file(packingDir().concat("binaries.json"))
    def json = new JsonSlurper().parseText(binJson.text)

    download {
        src([json.packr, json.windows32, json.windows64, json.linux64, json.mac])
        dest packingDir().concat("jdks")
        overwrite false
    }
}

task release() {
    dependsOn 'dist'
    dependsOn 'pack'
    dependsOn 'zipAll'
    dependsOn 'githubRelease'
    tasks.findByName('pack').mustRunAfter('dist')
    tasks.findByName("zipAll").mustRunAfter('pack')
    tasks.findByName("githubRelease").mustRunAfter('zipAll')
}

githubRelease {
    //token project.property('github.token')
    owner "ao-libre"
    repo "ao-java"
    targetCommitish "master"
    draft false
    prerelease true
    releaseAssets project.file('build/zip/').listFiles()
    apiEndpoint "https://api.github.com"
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher{
        name = 'Finisterra Argentum Online Client'
        if (osName().contains('mac')) {
		jvmArgs += '-XstartOnFirstThread'
	}
    }

    jpackage {
        jpackageHome = "${projectDir}/packing/jdks/jpack/bin"
        outputDir = releasesDir()
        imageName = 'FinisterraAO'
        skipInstaller = true
    }
}
